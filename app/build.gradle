apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {

    compileSdkVersion rootProject.ext.compileSDK

    defaultConfig {
        applicationId "br.ufs.uolchallenge"
        minSdkVersion rootProject.ext.minSDK
        targetSdkVersion rootProject.ext.targetSDK
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../extras/uol-keystore")
            storePassword "123456"
            keyAlias 'uolalias'
            keyPassword "123456"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
        }
    }

    flavorDimensions "envs"

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
            multiDexEnabled = true
        }
        live
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation project(':data-infrastructure')
    implementation project(':domain')
    implementation project(':presentation')

    def mobileUIDependencies = rootProject.ext.mobileUIDependencies
    def mobileUITestDependencies = rootProject.ext.mobileUITestDependencies

    implementation mobileUIDependencies.kotlin
    implementation mobileUIDependencies.kotlinReflect
    implementation mobileUIDependencies.gson
    implementation mobileUIDependencies.rxJava
    implementation mobileUIDependencies.rxAndroid
    implementation mobileUIDependencies.appCompat
    implementation mobileUIDependencies.cardView
    implementation mobileUIDependencies.recyclerView
    implementation mobileUIDependencies.design
    implementation mobileUIDependencies.archRuntime
    implementation mobileUIDependencies.archExtensions
    implementation mobileUIDependencies.picasso

    testImplementation mobileUITestDependencies.junit
    testImplementation mobileUITestDependencies.assertj
    testImplementation mobileUITestDependencies.roboletric

    androidTestImplementation mobileUITestDependencies.espressoCore
    androidTestImplementation mobileUITestDependencies.espressoRules
    androidTestImplementation mobileUITestDependencies.espressoRunner
    androidTestImplementation (mobileUITestDependencies.barista) {
        exclude group: 'com.android.support'
    }
}


configurations.all {
    resolutionStrategy {
        forcedModules = [
                rootProject.ext.forceResolution.kotlin,
                rootProject.ext.forceResolution.kotlinReflect,
                rootProject.ext.forceResolution.espressoCore,
                rootProject.ext.forceResolution.espressoRunner,
                rootProject.ext.forceResolution.espressoRules
        ]
    }
}